buildscript {
	ext {
        groovyVersion = '2.4.14'
        groovyHttpBuilderVersion = '0.7.1'
        jsr305Version = '3.0.2'
        lombokVersion = '1.16.20'
        spockVersion = '1.1-groovy-2.4'
		springBootVersion = '2.0.0.RELEASE'
		springVersion = '5.0.4.RELEASE'
        swaggerVersion = "2.6.1"
    }
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
	}
}

configure(allprojects) {
    apply plugin: 'java'
    apply plugin: 'idea'

    group 'com.dwalczak'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }
}

subprojects {
    dependencies {
        if (project.name != 'model') {
            compile "org.springframework:spring-context:$springVersion"
            compile project(':model')
        }
        compile "com.google.code.findbugs:jsr305:$jsr305Version"
        compileOnly "org.projectlombok:lombok:$lombokVersion"
    }
}


project('adapter-newsapi') {
    dependencies {
        compile "org.springframework:spring-web:$springVersion"
    }
}

project('service') {
    dependencies {
        compile project(':adapter-newsapi')
    }
}

project('web-rs') {
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile project(':service')
        compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
        compile "io.springfox:springfox-swagger2:$swaggerVersion"
        compile "io.springfox:springfox-swagger-ui:$swaggerVersion"
        testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"
    }
}

project('test-rs') {
    apply plugin: 'groovy'
    sourceSets {
        integrationTest {
            groovy.srcDir 'src/integration/groovy'
        }
    }
    dependencies {
        integrationTestCompile "org.codehaus.groovy:groovy-all:$groovyVersion"
        integrationTestCompile "org.codehaus.groovy.modules.http-builder:http-builder:$groovyHttpBuilderVersion"
        integrationTestCompile "org.spockframework:spock-core:$spockVersion"
    }
}

SwaggerTasks.addTasks(project)